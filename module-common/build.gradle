buildscript {
    ext {
        queryDslVersion = "5.0.0"
    }
}
plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.14'
    id 'com.ewerk.gradle.plugins.querydsl' version '1.0.10'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

group = 'com.cstudy'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '11'
}

repositories {
    mavenCentral()
}

dependencies {
    //Spring
    implementation('org.springframework.boot:spring-boot-starter-web')

    implementation 'org.springframework.boot:spring-boot-starter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    //JPA
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    //MongoDB
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'

    //QueryDSL
    implementation "com.querydsl:querydsl-jpa:${queryDslVersion}"
    implementation "com.querydsl:querydsl-apt:${queryDslVersion}"

    //gson
    implementation 'com.google.code.gson:gson'

    //Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    //valid
    implementation 'org.springframework.boot:spring-boot-starter-validation'

}

tasks.named('test') {
    useJUnitPlatform()
}


/*
[tasks.bootJar 기본 값 : true]

`bootJar` 옵션을 true로 설정하면 'xxx.jar' 파일이 생성된다.
ex) module-common-0.0.1-SNAPSHOT.jar
이렇게 생성된 'jar' 파일은
그 파일 안에 Application을 실행시키는 데 필요한
[dependencies / classes / resources]을 포함하고 있어
`java -jar` 옵션으로 jar 파일을 실행시킬 수 있다.

그런데 Common Module은
다른 Module에서 참조하는 목적의 Module이므로
실행 가능한 jar 파일을 생성할 필요가 없다.
그러므로 `ModuleCommonApplication.class`는 존재할 필요가 없다.
그러므로 Common Module에서는
bootJar 옵션값을 false로 설정한다.

만약 bootJar 옵션을 true로 주면
Main.class를 찾게 되는데
Common Module에서는 Api Module처럼
ModuleApiApplication.class가 없으므로 다음과 같은 에러가 발생한다.
ex) Caused by: org.gradle.api.InvalidUserDataException: Main class name has not been configured and it could not be resolved
 */
tasks.bootJar { enabled = false }

/*
[tasks.jar 기본 값 : true]

`jar` 옵션을 true로 설정하면 'xxx-plain.jar' 파일이 생성된다.
ex) module-common-0.0.1-SNAPSHOT-plain.jar
이렇게 생성된 '-plain.jar' 파일은 'jar' 파일과는 다르게
'dependencies'를 제외한 [classes / resources]만을 포함하고 있어
`java -jar` 옵션으로 jar 파일을 실행시킬 수 없다.

Common Module에서는 Api Module에서 사용할
[classes / resources]만 존재하면 되므로
jar 옵션값을 true로 설정한다.
 */
tasks.jar { enabled = true }

// ./gradlew clean :module-api:buildNeeded --stacktrace --info --refresh-dependencies -x test


def querydslDir = "$buildDir/generated/querydsl"

querydsl {
    jpa = true
    querydslSourcesDir = querydslDir
}

sourceSets {
    main.java.srcDir querydslDir
}

compileQuerydsl {
    options.annotationProcessorPath = configurations.querydsl
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    querydsl.extendsFrom compileClasspath
}